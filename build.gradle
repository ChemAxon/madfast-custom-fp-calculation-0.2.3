/*
 * Copyright 2017 ChemAxon Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'java'


ext {
    // MadFast version to be modified
    madfastVersion = '0.2.3'
}

dependencies {
    // Will depend on MadFast distributions's classpath
    // For compilation we could depend on the classpath.jar, however certain IDEs may have problem with dependencies
    // referenced only by such a pathing jar
    // See https://docs.gradle.org/current/userguide/dependency_management.html#sub:file_dependencies
    compile fileTree(dir: "../madfast-cli-$madfastVersion/lib/", include: '*.jar')
}

// Create a jar which references in its Manifest Class-Path attribute the pathing jar from the MadFast distribution and
// the built jar from this project
task extendedPathingJar(type: Jar) {
    dependsOn jar
    archiveName = 'extendedClasspath.jar'
    doFirst {
        manifest {
            attributes "Class-Path": [ jar.archivePath.getName(), "../../../madfast-cli-$madfastVersion/lib/classpath.jar" ].join(' ')
        }
    }
}

// Copy the launcher scripts from the MadFast distribution into build/bin and modify them to use the extended pathing
// jar in this project.
task extendedBins(type: Copy) {
    description "Create modified MadFast launcher scripts in build/bin which include the custom fingerprint from this project"
    group "Build"
    dependsOn extendedPathingJar
    from "../madfast-cli-$madfastVersion/bin"
    into 'build/bin'
    // See https://docs.gradle.org/current/userguide/working_with_files.html#sec:filtering_files
    filter {
        String line -> line
            // Classpath reference
            // Note that APP_HOME (in the launched scripts) will be relative to the modified script
            .replaceAll( 
                "lib/classpath.jar",  // The original pathing jar reference from the MadFast distribution
                "libs/extendedClasspath.jar" // The relative path to the new pathing jar when launching from the new location 
            )
            // The webapp war file is not copied, need to reference from the original MadFast distribution
            .replaceAll(
                 "APP_HOME/lib/overlap-examples-ws-$madfastVersion-webapp", 
                 "APP_HOME/../../madfast-cli-$madfastVersion/lib/overlap-examples-ws-$madfastVersion-webapp")
    }
}

